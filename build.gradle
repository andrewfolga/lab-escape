group 'co.tide'
version '1.0-SNAPSHOT'

apply plugin: 'java'
compileJava.options.compilerArgs << "-XDignore.symbol.file"

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

sourceSets {
    itest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/itest/java')
        }
        resources.srcDir file('src/main/resources')
    }
}

configurations {
    itestCompile.extendsFrom testCompile
    itestCompile.extendsFrom testRuntime
}

dependencies {
    compile group: 'com.sun.net.httpserver', name: 'http', version: '20070405'
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.3'
    compile group: 'commons-io', name: 'commons-io', version: '2.4'
    compile group: 'log4j', name: 'log4j', version: '1.2.17'
    compile group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.15'
    compile group: 'org.springframework', name: 'spring-core', version: '4.1.0.RELEASE'
    compile group: 'org.springframework', name: 'spring-context', version: '4.1.0.RELEASE'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.4'
    testCompile ('junit:junit:4.11') {
        exclude group: 'org.hamcrest', module: 'hamcrest-core'
    }
    testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
    testCompile group: 'org.mockito', name: 'mockito-all', version: '1.10.19'
    testCompile group: 'org.springframework', name: 'spring-test', version: '3.2.15.RELEASE'
}

task itest(type: Test) {
    testClassesDir = sourceSets.itest.output.classesDir
    classpath = sourceSets.itest.runtimeClasspath
}

check.dependsOn itest
itest.mustRunAfter test

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

task execute(type:JavaExec) {
    main = mainClass
    classpath = sourceSets.main.runtimeClasspath
}